MODULE ReceiptPrinterAddPOS;

REQUIRE PosDashboardPayment,  PosDashboard;

FORM exportOrder
    OBJECTS  invoice = Invoice PANEL
    PROPERTIES(invoice) READONLYIF readonly(invoice)
        dateTime, number, amount
    
    OBJECTS detail = InvoiceLine
    PROPERTIES(detail) READONLYIF readonly(invoice)
        nameItem, quantity, price, untaxedAmount
    FILTERS invoice(detail) = invoice
;

receipt_printer 'ЧЕК' (Invoice i) {
    APPLY;
    EXPORT exportOrder OBJECTS invoice = i JSON;

    LOCAL result = FILE();
    EXTERNAL HTTP 'http://localhost:5000/printer' PARAMS exportFile() TO result;

    LOCAL status = STRING[10]();
    LOCAL message = STRING[100]();
    IMPORT JSON FROM result() TO() status, message;
    IF NOT status() == 'success' THEN {
        MESSAGE 'Ошибка: ' + message();
    }
    formOk();
}

EXTEND FORM posPayment
    PROPERTIES receipt_printer(i) READONLYIF (change(i) < 0 OR notCashPaymentAmount() > amount(i)) AND paymentAmount()
;

DESIGN posPayment {
    TOOLBARRIGHT {
        MOVE PROPERTY(receipt_printer(i)) { fontStyle = 'bold'; }
    }
}